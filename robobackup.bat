@echo off

 

REM ROBOCOPY ERRORLEVEL  HANDLED VIA !ERRORLEVEL!
REM DETAILS: https://stackoverflow.com/questions/24866477/if-call-exit-and-errorlevel-in-a-bat

REM FEW, FEW COMMENTS IN THE CODE, BECAUSE IT IS ALMOST SELF-EXPLANATORY

setlocal enabledelayedexpansion 
REM delayed expansion makes the errorlevel check bit different
 


cls

set robocopy_erorlevel=-1

set ERRLOG=.\ERR.LOG

REM THE DRIVE FOR THE DESTINATION
set DRIVE=%2
REM CHECK IF NOT SET
if "%~2" equ "" set DRIVE=NOT SET

REM THE SHORT PATH FOR ROBOBACKUP. E.g., if the DRIVE is C and SHORTPATH is RBK, the DESTINATION will be C:\RBK
REM !!! IMPORTANT: IT IS HARDCODED TO AVOID LONG TEXT AND MINIMIZE 255 CHAR PATH ISSUES !!!
set SHORTPATH=RBK

set CURRENTSETTINGFILE=%1
REM REMOVE THE QUOTATION MARK " 
set CURRENTSETTINGFILE=%CURRENTSETTINGFILE:"=%
REM CHECK IF NOT SET
if "%~1" equ "" set CURRENTSETTINGFILE=NOT SET

set HEAD_DESTINATION=undefined

set DEFAULT_GENERAL_TIMEOUT=10

set GENERAL_TIMEOUT=%3

IF "" == "%3" (
set GENERAL_TIMEOUT=%DEFAULT_GENERAL_TIMEOUT%
echo.
echo       INFO:
echo.
echo.
echo No timeout for the questions has been set^.
echo Robobackup is going to use the default ^(%DEFAULT_GENERAL_TIMEOUT% seconds^).
echo.
echo Press M to Move on. 
choice /C M /D M /t %DEFAULT_GENERAL_TIMEOUT%

cls
)


REM COLOR VARIABLE SETTINGS
set NEXT_LABEL=undefined

set CL_GREEN=[1;92m
set CL_RED=[1;91m
set CL_WHITE=[0m
set CL_BLUETTE=[1;94m
set CL_CYAN=[1;36m
set CL_BRIGHT=[1;97m

set CL_YELLOW=[1;33m
set CL_GROON=[1;32m
 




REM BRIEF INFO
echo +-------------------------------------------------------+  
echo ^|      THIS VERSION MUST BE RUN AS ADMIN TO WORK        ^|
echo ^|         PROPERLY AVOIDING PERMISSION ISSUES           ^|
echo ^|                                                       ^|
echo ^|    following values, semicolon separated:             ^|
echo ^|                                                       ^|
echo ^|    -Origin path                                       ^|
echo ^|    -Destination path                                  ^|
echo ^|    -Description                                       ^|
echo ^|    -Short description for filenames                   ^|
echo ^|                                                       ^|
echo ^|    In order to change the backup log destination      ^|
echo ^|    please change the variable CurMainLogPath          ^|
echo +-------------------------------------------------------+



echo. 
echo. 
echo. 
 
echo Please choose if you need to proceed (A)utomatically, (M)anually or see the (H)elp
echo (after 10 seconds the app will moveon automatically)
choice /c AMH /d A /t 10

set curerrl=%ERRORLEVEL%
REM CHOICE IS H >> HELP
if %curerrl% EQU 3  goto show_help
REM CHOICE IS M >> MAIN MENU
if %curerrl% EQU 2  goto main_menu
REM ELSE  IF A (errorlevel 1) >>  PROCEED
if %curerrl% EQU 1  goto proceed_automatically




REM AUTOEXPLICATIVE
:main_menu
cls

echo.
echo.
echo.
echo %CL_WHITE% -- ROBOBACKUP MENU --
echo.
echo D^) Set the destination %CL_BLUETTE%D%CL_WHITE%rive (currently: %CL_GREEN%'%DRIVE%:'%CL_WHITE%)
echo.
echo L^) %CL_BLUETTE%L%CL_WHITE%ist the backups stored in the destination (currently: %CL_GREEN%'%DRIVE%:'%CL_WHITE%)
echo.
echo A^) Open the %CL_BLUETTE%A%CL_WHITE%dvanced menu
echo.
echo F^) Load a setting %CL_BLUETTE%F%CL_WHITE%ile (current file: %CL_GREEN%'%CURRENTSETTINGFILE%'%CL_WHITE%).
echo.
echo P^) %CL_BLUETTE%P%CL_WHITE%rints the current setting file content.
echo.
echo B^) Move on with the %CL_BLUETTE%B%CL_WHITE%ACKUP using the current settings.
echo.
echo X^) E%CL_BLUETTE%x%CL_WHITE%it.
echo.
echo H^) Show the %CL_BLUETTE%h%CL_WHITE%elp.
echo.
echo ^(if the app doesn't get an answer in 10 minutes, will exit.^).
echo.
choice /c DLAFPBXH /d X /t 600

set curerrl=%ERRORLEVEL%

REM CHOICE IS H >> HELP
if %curerrl% EQU 8  goto show_help

REM CHOICE IS X >> EXIT
if %curerrl% EQU 7  goto end

REM CHOICE IS B >> BACKUP
if %curerrl% EQU 6 goto do_the_BACKUP

REM CHOICE IS P >> PRINT
if %curerrl% EQU 5  goto print_settingfile

REM CHOICE IS F >> FILE SETTING
if %curerrl% EQU 4  goto load_settingfile

REM CHOICE IS A >> ADVANCED MENU 
if %curerrl% EQU 3  goto advanced_menu

REM CHOICE IS L >> LIST THE BACKUPS 
if %curerrl% EQU 2  goto list_available_backups

REM CHOICE IS D >> set the DESTINATION
if %curerrl% EQU 1 goto set_destination
GOTO:EOF


:advanced_menu

rem call :proceed_or_cancel
cls
echo ADVANCED MENU
echo . 

echo T^) Define a scheduled  ^(t^)ask in windows ^(not yet implemented)
echo M^) ^(M^)anage a scheduled task in windows ^(not yet implemented)


echo.
echo You can contribute implementing the missing features via github:
echo. 
echo %CL_BLUETTE%  https://github.com/trincio/robobackup    %CL_WHITE%  
echo. 
echo. 

echo ^(if the app doesn't get an answer in 10 minutes, will exit.^).
echo.
choice /c TMC /d C /t 600

set curerrl=%ERRORLEVEL%
 
REM CHOICE IS T >>  
if %curerrl% EQU 3  goto main_menu

REM CHOICE IS M >>  
if %curerrl% EQU 2  goto main_menu

REM CHOICE IS X >> 
if %curerrl% EQU 1 goto main_menu

GOTO:EOF

:set_destination
cls
echo. 

echo %curerrl%


echo          Now it is quite important to get the right 
echo             destination from the listed disks
echo    (naturally destination can be mapped remote shares)
echo. 

echo please wait while the wmic gets the system drives...
echo.
REM GETTING THE LIST OF DISKS VIA WMIC
wmic logicaldisk get description,name, caption, filesystem, freespace, providername



echo.
if "%DRIVE%" NEQ ""  echo ^(please notice a letterdrive has already been previously set:%CL_GREEN%  %DRIVE% %CL_WHITE%^)

set /p DRIVE="Select the drive from the ones listed above (letter only): "
echo %DRIVE%

REM dir %DRIVE%:
REM IF ERRORLEVEL 1 goto notworking_driveletter

if exist %DRIVE%:\%SHORTPATH%\ (
  echo %CL_BLUETTE%%DRIVE%:\%SHORTPATH%\%CL_WHITE% found. Lets proceeed.
  set /p "=Press any key to move on..."
  goto main_menu
) else (
  echo unfortunately there's no  %CL_RED%'%DRIVE%:\%SHORTPATH%\' %CL_WHITE% folder reachable by Robobackup.
  echo Robobackup needs a reachable drive ^(current choice: '%DRIVE%'^) AND a folder %SHORTPATH% inside.
  echo The reason behind this that could seem an annoyance, comes from the fact that 
  echo it is easier and more robust to have a specific folder name for the backups, 
  echo and having a short-name folder minimizes the 255 char length folder issue ^(this 
  echo considering also that Robobackup needs some subfolder for setting the FULL and delta/incrementals^).
  echo. 
  echo [1mWhat to do in a short to make Robobackup working? 
  echo Simply exit the app and create a '%DRIVE%:\%SHORTPATH%\' folder. Thank you.%CL_WHITE%
  echo.
  set /p "=Press any key to move on..."
  goto main_menu
)

GOTO:EOF


:manage_task
goto not_implemented
:define_task
goto not_implemented

:not_implemented
cls
echo.
echo.
echo.
echo.

echo %CL_BLUETTE%   **  Unfortunately this feature has not been implemented yet  ** %CL_WHITE%  
echo.
echo.
  set /p "=Press any key to move on..."
  goto main_menu
GOTO:EOF 
 

:show_help
goto intro_help
GOTO:EOF

:detailed_help


cls
echo %CL_RED%+-------------------------------------------------------------+%CL_WHITE% 
echo %CL_RED%^|   %CL_GREEN%                                                           %CL_RED%^|%CL_WHITE% 
echo %CL_RED%^|   %CL_GREEN%                Robackup Help                              %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%                                                           %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%Robobackup works in a quite self-explanatory way.          %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%Basically the command asks for:                            %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%1. a destination drive letter                              %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%2. the path of a setting file                              %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%                                                           %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%Robobackup then runs robocopy for each item in the set-    %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%ting file (the file MUST be ANSI, UTF not-allowed).        %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%                                                           %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%Robobackup, if the destination and the setting file are    %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%passed correctly, runs automatically on these settings.    %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%Otherwise it lets the user choosing  manually  the set-    %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%tings.                                                     %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%                                                           %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%Robobackup also works with the following syntax and pa-    %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%rameters:                                                  %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%                                                           %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_BLUETTE%Robobackup settingPath destinationDiskLetter timeout       %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%                                                           %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%The Robocopy used parameters are:                          %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%                                                           %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_BLUETTE%/Compress /Z /B /XO /fft /V /NP /R:3 /E /Z /W:5 /MT:32     %CL_RED%^|%CL_WHITE%
echo %CL_RED%^|   %CL_GREEN%                                                           %CL_RED%^|%CL_WHITE%
echo %CL_RED%+-------------------------------------------------------------+%CL_WHITE%  
  



  set /p "=Press any key to move to the main menu..."
  goto main_menu
GOTO:EOF

:list_available_backups
cls
echo Listing the %DRIVE%:\%SHORTPATH%\ folder (further improvements in the next releases)
echo. 
echo %CL_CYAN%

if exist %DRIVE%:\%SHORTPATH%\ (


	REM DIR ONLY NAMES, ORDERD BY NAMES AND PAGED
	dir /B /ON /P %DRIVE%:\%SHORTPATH%\

	echo %CL_WHITE%
	set /p "=Press any key to move on..."
	goto main_menu
) else (

	echo %CL_WHITE%
	echo unfortunately there's no %CL_RED%'%DRIVE%:\%SHORTPATH%\'%CL_WHITE% folder reachable by Robobackup.
	set /p "=Press any key to move back to the menu and define a drive letter..."
	goto main_menu
)
GOTO:EOF

:print_settingfile
cls
if exist %CURRENTSETTINGFILE% (

	set /p "=Press any key to read the file content..."

	more %CURRENTSETTINGFILE%

	set /p "=Press any key to go back to the menu..."

	goto main_menu
) else (

echo unfortunately  %CL_RED%'%CURRENTSETTINGFILE%'%CL_WHITE% setting file is not reachable by Robobackup.
set CURRENTSETTINGFILE=undefined
set /p "=Press any key to move back to the menu, define a different setting file or make a different choice..."
goto main_menu
)
 
GOTO:EOF


:load_settingfile
cls

echo Robobackup needs a setting file.
echo Here you can define it.
echo The current setting file is %CL_GREEN%'%CURRENTSETTINGFILE%'%CL_WHITE% ^(not yet set if empty^).
echo. 
set /p CURRENTSETTINGFILE="Type its path here: "
  
if exist %CURRENTSETTINGFILE% (

	set /p "=Press any key to read the file content..."

	more %CURRENTSETTINGFILE%

	set /p "=Press any key to proceed..."

	goto main_menu
) else (

echo unfortunately  %CL_RED%'%CURRENTSETTINGFILE%'%CL_WHITE% setting file is not reachable by Robobackup.
set CURRENTSETTINGFILE=undefined
set /p "=Press any key to move back to the menu, retry or make a different choice..."
goto main_menu
)
 
GOTO:EOF


:show-help
cls
echo Help
GOTO:EOF

:proceed_automatically
:do_the_BACKUP


cls
 
set HEAD_DESTINATION=%DRIVE%:\%SHORTPATH%\



if exist %HEAD_DESTINATION% (
echo. 
) else (

	echo unfortunately the destination %CL_RED%'%HEAD_DESTINATION%'%CL_WHITE% is not reachable by Robobackup.
	set HEAD_DESTINATION=undefined
	set /p "=Press any key to move back to the menu, retry or make a different choice..."
	goto main_menu
)



REM IN THE FILE PASSED TO THE CORE PART, WE'VE CURRENTLY 4 FIELDS:
rem SOURCE 
rem DESTINATION 
rem DESCRIPTION 
rem DESCRIPTION_FILENAME_DETAILS


cls
REM DETERMINING THE TOKENIZED DATA AND VARIABLES FOR TIMESTAMP


for /f "tokens=1,2,3,4 delims=;" %%a in (%CURRENTSETTINGFILE%) do (


	set SOURCE=%%a
	set DESTINATION=%%b
	set DESCRIPTION=%%c
	set DESCRIPTION_FILENAME_DETAILS=%%d
	
	REM REMOVE THE QUOTATION MARK " 
	set SOURCE=!SOURCE:"=!
	set DESTINATION=!DESTINATION:"=!
	set DESCRIPTION=!DESCRIPTION:"=!
	set DESCRIPTION_FILENAME_DETAILS=!DESCRIPTION_FILENAME_DETAILS:"=!
	


 
	
	set CurDate=!date:~6,4!!date:~3,2!!date:~0,2!
	set CurTime=!time:~0,2!.!time:~3,2!.!time:~6,2!


	REM ADD THE HEAD PATH TO THE DESTINATION
	set COMPLETE_PATH_DESTINATION=!HEAD_DESTINATION!!DESTINATION!\!CurDate!
	
	set CurMainLogPath=!HEAD_DESTINATION!LOGS\

	echo               PRELIMINARY CHECK

	echo.
	echo.	
	REM CHECK OF THE DESTINATION HAS ALREADY DONE. NOW TIME TO CHECK THE LOGS DIRECTORY (if missing create it)
	if exist !CurMainLogPath! (
	echo %CL_GREEN%!CurMainLogPath!%CL_WHITE% exists.%CL_WHITE%   
	echo.
	call :proceed_or_cancel

	
	) else (
	cls
	echo.
	echo.
	echo.
	echo.
	echo %CL_RED%'!CurMainLogPath!' doesn't exist. The proposed option is to proceed with creating one.%CL_WHITE% 
	echo.
	echo.
	echo.
	call :proceed_or_cancel
	mkdir !CurMainLogPath!
	
	)	
 
	 
 

	REM HERE THE QUOTATION MARK, IN THE FINAL PATH, ARE NEEDED TO AVOID ANY ISSUE WITH SPACES IN PATH NAMES
	set CurMainLogFile="!CurMainLogPath!!CurDate!-!CurTime!_robobackup.MAIN.LOG"
	set CurDetailedLogFile="!CurMainLogPath!^(!DESCRIPTION_FILENAME_DETAILS!^)!CurDate!-!CurTime!_robobackup.DETAILS.LOG"

	
	REM ALSO THE SOURCE NEEDS THE QUOTATION (PREVIOUSLY REMOVED AT THE MOMENT AS A STANDARD BEHAVIOR)
	set SOURCE="!SOURCE!"

	REM ALSO THE DESTINATION NEEDS THE QUOTATION (PREVIOUSLY REMOVED TO AVOID ANY ISSUE WITH SPACES IN PATH NAMES)
	set COMPLETE_PATH_DESTINATION="!COMPLETE_PATH_DESTINATION!"

 
	cls
 
 
	echo Below you can find the log files path for further analysis in case of any issue:

	echo - MAIN LOG FILE: %CL_BLUETTE%!CurMainLogFile!%CL_WHITE%
	echo - DETAILED LOG:  %CL_BLUETTE%!CurDetailedLogFile!%CL_WHITE%
	

	echo.
	
	echo And here you can find the data used for this backup task:
	
	echo - SOURCE:         %CL_BLUETTE%!SOURCE!%CL_WHITE%
	echo - SOURCE:         !SOURCE! >> !CurMainLogFile!
						 
						 
						 
	echo - DESTINATION:    %CL_BLUETTE%!COMPLETE_PATH_DESTINATION!%CL_WHITE%
	echo - DESTINATION:    !COMPLETE_PATH_DESTINATION!	>> !CurMainLogFile!
	echo - DESCRIPTION:    %CL_BLUETTE%!DESCRIPTION!%CL_WHITE%
	echo - DESCRIPTION:    !DESCRIPTION! >> !CurMainLogFile!



	echo - DESCR. DETAILS: %CL_BLUETTE%!DESCRIPTION_FILENAME_DETAILS!%CL_WHITE%
	echo - DESCR. DETAILS: !DESCRIPTION_FILENAME_DETAILS! >> !CurMainLogFile!
 

	
	echo. 
	echo. >> !CurMainLogFile!
	echo --------- PROCESS BEGINNING at %CL_BLUETTE%!CurDate!-!CurTime!%CL_WHITE% for the backup '%CL_BLUETTE%!DESCRIPTION!%CL_WHITE%' --------- 
	echo --------- PROCESS BEGINNING at !CurDate!-!CurTime! for the backup '!DESCRIPTION!' --------- >> !CurMainLogFile!
 	echo.
	
 	IF EXIST !SOURCE! (
	echo SOURCE: %CL_GREEN%found%CL_WHITE%^(!SOURCE!^)%CL_WHITE%
	echo SOURCE: !SOURCE!  has been found >> !CurMainLogFile!
	echo.
	) ELSE (
	echo Problems reaching %CL_RED%!SOURCE!%CL_WHITE%. Probably the process will be skipped. 
	echo It is up to you try to move on or not.
	echo If you're not completely aware about what you're doing, please cancel the operation.
	echo Problems reaching !SOURCE!. Probably the process will be skipped >> !CurMainLogFile!
	)


	call :proceed_or_cancel
	
	cls

	REM RELEVANT CHECK: IF THE DESTINATION ALREADY EXISTS IT COULD BE A PROBLEM
	IF EXIST !COMPLETE_PATH_DESTINATION! (
 
	echo %CL_CYAN%'       THE FOLDER %CL_RED%'!COMPLETE_PATH_DESTINATION!'%CL_CYAN% ALREADY EXISTS!%CL_WHITE%'
	echo.
	echo What does that mean? Maybe you have already launched the backup today.
	echo.
	echo Maybe you really need to launch again the Robobackup with such exact destination BUT
	echo this would be an aware action, and to be sure it is aware you're asked to launch 
	echo manually the robocopy command.
	echo.
	echo Please be sure you UNDERSTAND what does mean launching the following command before
	echo launching it.
	echo.
	echo %CL_RED% robocopy !SOURCE!  !COMPLETE_PATH_DESTINATION!  /Compress /Z /B /XO /fft /V /NP /R:3 /E /Z /W:5 /MT:32 /LOG+:!CurDetailedLogFile! %CL_CYAN%
	echo.
	echo.


	
	call :proceed_or_cancel
	 
	)
 
	
	echo !CurDate!-!CurTime! details will be written here: '!CurDetailedLogFile!'
	echo !CurDate!-!CurTime! details will be written here: '!CurDetailedLogFile!' >> !CurMainLogFile!


	call :proceed_or_cancel
	cls

 
	set timestamp=!date:~6,4!!date:~3,2!!date:~0,2! !time:~0,2!.!time:~3,2!.!time:~6,2!
	echo !timestamp! BEGINNING THE ROBOCOPY BETWEEN THE AFOREMENTIONED FOLDERS >> !CurMainLogFile!
	
	echo *** RUNNING THE FOLLOWING COMMAND:
	echo robocopy !SOURCE!  !COMPLETE_PATH_DESTINATION!  /Compress /Z /B /XO /fft /V /NP /R:3 /E /Z /W:5 /MT:32 /LOG+:!CurDetailedLogFile! >> !CurMainLogFile!
	echo
	robocopy !SOURCE!  !COMPLETE_PATH_DESTINATION!  /Compress /Z /B /XO /fft /V /NP /R:3 /E /Z /W:5 /MT:32 /LOG+:!CurDetailedLogFile!

	REM set robocopy_erorlevel=!ERRORLEVEL!
	set robocopy_erorlevel=ERRORLEVEL
	echo robocopy has exited with errorlevel: !robocopy_erorlevel!
	
	
	
 
	set timestamp=!date:~6,4!!date:~3,2!!date:~0,2! !time:~0,2!.!time:~3,2!.!time:~6,2!
	echo !timestamp! FINISHED THE ROBOCOPY BETWEEN THE AFOREMENTIONED FOLDERS - ANY FURTHER MESSAGES IN THE LINES BELOW>> !CurMainLogFile!
	
	echo. >> !CurMainLogFile!
 

	IF !robocopy_erorlevel! EQU 0 (
		echo No issues but same files in the destination directory has been found. No action taken. Means: no copy. >> !CurMainLogFile!
	) 
	
	if !robocopy_erorlevel! EQU 1 (
		echo All selected files copied. >> !CurMainLogFile!
	) 
	
	if !robocopy_erorlevel! EQU 2 (
		echo Destination contains files not in the origin. No files copied. See the detailed log. >> !CurMainLogFile!
	) 
	
	if !robocopy_erorlevel! EQU 4 (
		echo Some files copied. The destination contains mismatched files. See the detailed log. >> !CurMainLogFile!
	) 
	
	if !robocopy_erorlevel! EQU 8 (
		echo Some files or directories could not be copied and the retry limit was exceeded. . See the detailed log. >> !CurMainLogFile!
	) 
	
	if !robocopy_erorlevel! EQU 16 (
		echo Robocopy did not copy any files.  Check the command line parameters and verify that Robocopy has enough rights to write to the destination folder. >> !CurMainLogFile!
	) 


)

echo                --------- PROCESS COMPLETED --------- 
echo                --------- PROCESS COMPLETED --------- >> !CurMainLogFile!

setlocal disabledelayedexpansion

echo.
echo.
echo BACKUPS COMPLETED (see the logs)..... 

GOTO:EOF 





:notworking_driveletter
echo Error reading %DRIVE%. Please check, then restart the app.
timeout /T %GENERAL_TIMEOUT%
GOTO:EOF


:proceed_or_cancel


REM if %NEXT_LABEL%==undefined (

	REM echo there's probably a development issue because the NEXT_LABEL variable has not been set properly.
	REM echo EXITING THE APP
	REM exit /b 1
	
REM )

echo %CL_BRIGHT%Would you want to proceed?%CL_WHITE%
echo.
echo %CL_BLUETTE%Y%CL_WHITE%=Yes, %CL_BLUETTE%C%CL_WHITE%=Cancel, default: Y (will be chosen in %GENERAL_TIMEOUT% seconds).
echo.
choice /C YC  /D Y /t %GENERAL_TIMEOUT%
REM if %ERRORLEVEL%==1 goto %NEXT_LABEL%
REM if /i erorlevel==2 goto main_menu
REM if /i erorlevel==1 GOTO:EOF

REM if %ERRORLEVEL%==2 GOTO:EOF
REM if %ERRORLEVEL%==1 goto main_menu

set curerrl=%ERRORLEVEL%
if %curerrl% EQU 2 goto main_menu
if %curerrl% EQU 1 GOTO:EOF

GOTO:EOF

 
 
:intro_help


REM in the picture remember to escape also the ^ and the +, other than the | and the > or <, (, ), !, ", comma, backthick, &, '

REM the % must be escaped with double %
cls


echo  .
echo                                       %CL_GREEN%        _____                   %CL_BRIGHT%    www.trincio.com          
echo                                       %CL_GREEN%      _Â¡gÂ¶Â¶@@ÃŠ^^^^^^Â¶Â¶ggÂ¡_      %CL_BRIGHT%    www.gabrielemotta.com        
echo                                       %CL_GREEN%   _ÂµPÂ¶Â¯Â¯Â¯^^Â°Ã‘L   Â¶@@@@Â¶Â¶Â¡_     %CL_BRIGHT%                                 
echo                                       %CL_GREEN% Â¸Â¶ÃŠ  Â¨L     _   Ã‘Ã‘Â¶@@Â¶  ^^q_   %CL_BRIGHT%      Robobackup 1.0                        
echo                                       %CL_GREEN%p^^ Ã…_  ]     L  J   Â¯Ã‘Â¶   Â¸Â¶Ã§  %CL_BRIGHT%      _                          
echo                %CL_BRIGHT%       _      Â¡_     %CL_GREEN%_Â¶    )  Â¨_    L  Ã    ]Â¯  ,Â¶@Â¶Â§%CL_BRIGHT%gÂ¡Â¡Â¡__Â¶`Ã§    Â¡~q                  
echo                %CL_BRIGHT%      Â¶^^Â¼____jÂ¡^^Â¼Ã¦w=Âª%CL_GREEN%Â°[     \  \    L  Ãˆ    ÃŠ  ,Ã•Â¶@@@Ãˆ%CL_BRIGHT%Â¯Â¯Â¯Â¯Ã‘Â¯Â°^^^^Â°Â°Â°ÃŠ,wÂ¶mmÃ¦q_            
echo                %CL_BRIGHT%Â¸Â¡Ã¦^*wÂ¶Â¶~ ÃŽ^^Â¯Â¯Â¯ ^|0L  %CL_GREEN%  Â¶Ã§     L J_   L J    j  y^^qÂ¶@@Â¶L%CL_BRIGHT%    Â¶ L     L]     [Â¶            
echo               _%CL_BRIGHT%Â¶Â¶       [     J L  %CL_RED% J@%CL_GREEN%Â¶Â§     L [   ^| ^|   ,^^ JÂ¯_Â¶@@@^|[%CL_BRIGHT%     W      \]     [Â¶%CL_YELLOW%gÂ¡_         
echo          _Â¡yÂ¶Ã‘Ã‘%CL_BRIGHT%Ã‘Ã‘      Â¿Â¶     Â¨Ã½   %CL_RED% Â¶Â¯ÃŠ%CL_GREEN%Â¶Â¶_    ,"_  ^| Ãˆ   Â¶ /Â¯,Â¶@@Â¶Ã‘Â¶%CL_RED%[%CL_BRIGHT%     `      Â¨Â¶     [Â¶Ã‘Ã‘Ã‘%CL_YELLOW%Â¶Â¶ggÂ¡_   
echo      _Â¡yÂ¶Â°Â¯    %CL_BRIGHT% Ã‹r     ^^       Â¯  %CL_RED%  J Â¯Ã‘@%CL_GREEN%Â¶Ã§    ,\  Â¬ L  0 / gÂ¶@@@Â¶Ã‘%CL_RED%Â¶Â¶%CL_BRIGHT%                   [Â¶   %CL_YELLOW%  Â¯^^Ã‘Ã‘Â¶gÂ¿
echo   Â¼gÂ¶Â¶ÃŠ_       %CL_BRIGHT% Â¶[                 %CL_RED%  L  Â¶Â¶%CL_GREEN%Ã‘Â¶_   \L  J  Â¸Ã‘` g@Â¶Ã…Ã‘@%CL_RED% MÃ‘[ %CL_BRIGHT%                  Ã‡ÃŠ    %CL_YELLOW%    _Â¡Ã½Â¶
echo        Â¯Â°ÂªwÂ¡_  %CL_BRIGHT% Â¯Ã‘Â¶Âµ,_             %CL_RED%  Â¶Â¡Â¡@Â¶%CL_GREEN% Â¯Ã‘Ã§   Ã¢  Ã‰  Ã½Â´Â¡Â¶@Â¶Â¯ _@%CL_RED%@gÂ¶L %CL_BRIGHT%            _._Ã†r^^     %CL_YELLOW%_Â¡gÂ¶Â¶Â°Â¯  
echo            Â¯^^Â¶Ã§_   %CL_BRIGHT%^^ÃžÃƒ~Â»,_       %CL_RED%    Â¶Â¯Â°Ã‘Â¶y_ %CL_GREEN%^^Â¶Â¡_ ______Â¶@Â¶Â¯ %CL_RED%_gÂ¶Ã‘^^0Ã•Â¸  %CL_BRIGHT%       Â¸.-Â¡Ã¦~Â¯     %CL_YELLOW%_gÂ¶@Â¶Â°Â¯     
echo               Â¯Ã‘Â¶Â¡   %CL_BRIGHT% Â¯^^^*ÃŠÂ¶v,_   %CL_RED%   MÂ©^>,  Â¯Ã‘Â¶gÂ¡%CL_GREEN%Â¶@Â¶@@@@@@@ÃŠÂ¡%CL_RED%gÂ¶Ã‘Â¯  Â«$Â£Â¶   %CL_BRIGHT%  _.r_gÂ¶Â¶Â¯  %CL_YELLOW%    _Ã¦Â¶@Â¶Â¯        
echo                  Ã‘Â¶Â¡      Ã‘Â¶gÂ¡Â°%CL_BRIGHT%Â»,_ %CL_RED%  L '`Â¬   Â¯Â°Ã‘Â¶Â¶Â¶Â¯Â¯Â¯Â°%CL_RED%Ã‘Â¶Â¶Ã‘Â¯  Â¸z^>~%CL_BRIGHT% _Â¶ ,Â«Â³^^Â¡Ã¦Â°%CL_YELLOW%@@Â¶Â¨      0Â¶@Â¶Â¯          
echo                   Â¯Â¶Â¶Â¸     Ã‘@@Â¶^*%CL_BRIGHT%Â«Â¯~Â«Â¡JÂ¾Â¸ %CL_RED%`=Z^+Â¸_ %CL_CYAN%Â¶ ]Â¸Â¸_[ 0%CL_RED%Â¶ Â¸Â«Â¢Â³Â¯ %CL_BRIGHT% pÂ¶P^^_Ã¦Â¶^^  %CL_YELLOW%Â¶@@ÃŠ     _Â¶@@Ã…            
echo                     Ã‘Â¶Â§     Â¶@@Â§ Â¯%CL_BLUETTE%Â°Â¶%CL_BRIGHT%Â¡Ã…Â¶Â¯4Â¡ %CL_RED% `=^^Â¬%CL_CYAN%Â¶c,   Â¯NÂ¶%CL_RED%Ã£^<Ã¨^^%CL_BRIGHT%  _pÂ°_Â¶%CL_BLUETTE%gÂ¶Â¯%CL_YELLOW%    _@@Â¶     _Â¶@@ÃŠ             
echo   ygggggggÂ¡_         0Â¶Â¶Â¡    Â¶@@L %CL_BLUETTE% J@%CL_BRIGHT%@Ã‘  Â¯Â³,   ,%CL_CYAN%Â¨Â¨^^Â¨Â¨Â¨  Â¨%CL_BRIGHT%Â¯   Â¸Ã¦Â´ Â¸ÃŠÂ¶%CL_BLUETTE%@Â¶  %CL_YELLOW%   Â¶@@ÃŠ     Â¶@@ÃŠ        ,Â«-~~Â°
echo   @@@@@@@Â¶Â¶@Â¶Â¶Â¡_      ^^@Â¶Â¡   Â¨Â¶@Â¶%CL_BLUETTE%  Â¯@@%CL_BRIGHT% `Â¡   Â°, ]          %CL_BRIGHT% ÂµÂ°Â¯  gÃŠ%CL_BLUETTE%wÂ¶@Â¶  %CL_YELLOW%  w@@Â¶     Â¶@@ÃŠ     Â¸Â«"Â¯      
echo   @@@@@@@@@@@@@Â¶Â¶Â¡_    Ã‘@@Â§   Ã‘@@Â§%CL_BLUETTE%  Â¶@[%CL_BRIGHT% Â¯Â¼_  Â¯Ã§]_Â¶~~~!Â¶Â¸M Ã‰   %CL_BRIGHT%_Â¶Â¯ %CL_BLUETTE%Â¶@@ÃŠ   %CL_YELLOW% Â¶@@Ãˆ    _Â¶@Â¶    Â¡Â¶^^       __
echo   Â¯^^Â¯Ã‘@@@Â¶Â°Ã‘Ã‘Â¶@@@@Â¶y    Ã‘@@Â§   Â¶@@Ã§ %CL_BLUETTE%Ã‘@Â¶Ã§%CL_BRIGHT%  Ã‘Â¡  "g/      `MÃ   yÃ‰  %CL_BLUETTE%Â¶@@Â¶Â¨  %CL_YELLOW%  @@Â¶     Â¶@@Ãˆ  Â¡Â¶@@Â¶_Â¡Â¡Â¶@Â°Â°^^^^
echo       Ã‘Â¶@Â¶Â¶_  Â¯Ã‘@@@@Â¶Â¸   Â¶@@L  Â¯Â¶@Â¶  %CL_BLUETTE%Â¶@Â¶Ã§ %CL_BRIGHT%Â¶Â¯Â°Â¡ Â¯        `  Â¡PÂ¶  %CL_BLUETTE%Â¶@@Â¶Â¯    %CL_YELLOW%Â¶@@ÃŠ     Â¶Â¯Â¶  gÂ¶@@@Â¶Â¶Â°Ã£Â¯     
echo      __Ã½@@@Â¶Â¶Â¶gÂ¶@@@@[Â¯   0ÃŠÂ¯Â¶   Ã‘@@L %CL_BLUETTE%Â¯Â¶@@Â¶Â¶  %CL_BRIGHT% Â³w_       ],Â¶Â¯ %CL_BLUETTE%J_Â¶@@Â¶^^    %CL_YELLOW% Â¶@@Â¨     L Â¶ gÂ¶@@Â¶Â¯  pÂ¯      
echo   ggÂ¶@@@@@@@@@@@@@@Â¶Â¶     Â¶ Â¯L   Â¶@Â¶_%CL_BLUETTE%  Ã‘@@Â¶Â§Â¸  %CL_BRIGHT% Â¯Â¯^^^^Â¶Â°^^^^Â¯ %CL_BLUETTE%   g@@Â¶Ã…Â¯  %CL_YELLOW%    Ã‘Â¶Â¶      L Â¶_Â¶@Â¶ÃŠÂ¯wÂ¶Â¡p=!~~==Â¬
echo   @@@@@@@@@@@@@@@@@@Â¶Ã§    Â¶p ^^   JÂ¶@[   %CL_BLUETTE%`Â¶Â¶@Â¶Â¶g_    Â¶    _Â¡Â¶Â¶@Â¶Ã‘Â¯       %CL_YELLOW% Â¯Â¶ÃŠ      [_Â¶Â¶@Â¶Â¯Â¡gÂ¶@@Â¶       
echo   @@@@Â¶Ã‘Ã‘$Ã…Ã‘Â¶Â°Â°Ã‘Â¶Ã‘Ã‘Ã‘Â¶Â¶    JÂ§      Â¶Ã‘Â¶    %CL_BLUETTE% Â¯Ã‘Â¶@@Â¶Â¶gÂ¡Â¡Â¶Â¡ggÂ¶Â¶@Â¶Â¶ÃŠÂ¯        %CL_YELLOW% Â¶ Â¶L      Â¶@@Â¶Â¶Â¡Â¶Â¶Ã‘Ã‘Ã‘Â°Â¶Â¶Â¶Â¶Â¶Â¶Â¶Â¶
echo   Â¶Â¶ÃŠ_yÂ¶Â°Â¯    Â£Â¯     Â¯Â¹    ÃŠ      Â¶  r   %CL_BLUETTE%    Â¯Â°Ã‘Â¶@@@@@@Â¶Ã‘Ã‘^^Â¯            %CL_YELLOW%  Â¶       Â¶Â¶Ã Ã‘Ã•Â¯Â¯Â¡Â¶Ã‘Â¯Â¯    Â¯4_ 
echo   _gÂ¶^^        Â¶                    L Ã¬  %CL_BLUETTE%          Ã‘@@@Â¶                 %CL_YELLOW%  ]      Â¡Â¶^^  Â¡Ã¦Â°Â¯        _pÂ¨ 
echo   Ã‘Â¯          Â¯!                   Âª "   %CL_BLUETTE%          Ã‘Ã‘Ã‘F                  %CL_YELLOW% Â´    -Â°Â¯   Â°Â¯          Âª^^   
echo. 
choice /C P /D P /t 5
goto detailed_help

GOTO:EOF


:end
cls
	echo.
	echo.
	echo.
	echo.
	echo %CL_CYAN%       ROBOBACKUP HAS COMPLETED ITS ACTIVITIES    %CL_WHITE%
	echo %CL_CYAN%       ANY ERROR DETAIL COULD BE FOUND IN THE     %CL_WHITE%
	echo %CL_CYAN%              LOG FILES FOR FURTHER               %CL_WHITE%
	echo %CL_CYAN%                      ANALYSIS                    %CL_WHITE%
	echo.          
echo.	           
	echo %CL_CYAN%          IF YOU ARE A DEVELOPER OR A SYS         %CL_WHITE%
	echo %CL_CYAN%               ADMIN AND YOU SUSPECT              %CL_WHITE%
	echo %CL_CYAN%              A BUG, WE'D BE PLEASED              %CL_WHITE%	
	echo %CL_CYAN%             IF YOU REPORT A SOLUTION             %CL_WHITE%	
	echo %CL_CYAN%                 TO THE ROBOBACKUP                %CL_WHITE%	
	echo %CL_CYAN%                    GITHUB PAGE                   %CL_WHITE%	
	echo %CL_CYAN%      https://github.com/trincio/robobackup       %CL_WHITE%	
echo.	           
	echo %CL_CYAN%                               thank you.         %CL_WHITE%	
	echo.
	echo.
	echo.

rem Currently a GOTO:EOF returns to the caller subroutine, that currently produces errors
rem in case of less basic process flows. The workaround is the following. TODO: implement a better
rem flow handling.
 set /p "=Press any key to exit..."
exit
   
